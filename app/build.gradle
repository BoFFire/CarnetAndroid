apply plugin: 'com.android.application'

static def getDate() {
    // we increment version number every 1000 seconds,
    // we start on 31/08/2016
    def date = new Date().getTime() / 1000L - 1472637600L
    return (int) (date / 1000L)
}

static def getFormattedDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd.HHmm')
    return formattedDate
}

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'
    dexOptions {
        javaMaxHeapSize "4g"
    }
    defaultConfig {
        applicationId "com.spisoft.quicknote"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode getDate()
        versionName '0.1-' + getFormattedDate()
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            resValue "string", "app_name", "QuickNote"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "QuickNote debug"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    flavorDimensions "version"
    productFlavors {
        fdroid {
            dimension "version"
            versionCode 1
			versionName '0.1'
        }
        google {
            dimension "version"
        }
    }
}

if (file("../../Internal").exists()) {
    apply from: '../../Internal/internal_build.gradle'
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'com.android.support:cardview-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.google.android:flexbox:0.3.1'
    compile project(':Sync')
    googleCompile 'com.google.android.gms:play-services:11.8.0'
    googleCompile project(':GoogleSync')
}
